10 REM D.M.C. SOFTWARE PRIMORA IRTA SZARKA ZOLTÁN
11 POKE16452,128
20 CLS:PRINT"A program eredetijét, 'A hős lovag'-ot,   Tihor Miklós írta C16; Plus/4-re 1985-ben.Az első kereskedelmi forgalomba került    magyar nyelvű kalandjátékként 1986-ban    ";:PRINT"adta ki a Novotrade (250Ft). Később módo- sította valaki a programot:";
21 PRINT" ékezet nélkülilett (mert macerás begépelni Commodore gé-pen), jobb prompt, és sokkal nehezebb erdőlett. Ez alapján születtek más ver";
25 PRINT"ziók is,néha 'A BATOR LOVAG' címmel (HOS helyett).Szarka Zoltán is egy ilyet írt át Primora,nem változtatva a játékmeneten, de a      használható igéket kilistázva, valamint a szöveget jóval mókásabbra cserélve. :)":PRINT
30 PRINT"NYOMJON MEG VALAMIT";
40 IF INKEY$=""THEN40
50 CLS:PRINT"Én javítottam két Szarka Zoltán által el- követett BUGot, pár elgépelést, könnyítet-tem az erdőn, lassítottam a begépelést, megírtam ezt a szöveget és új töltőképernyőtrajzoltam.":PRINT"Gona 2021.03.02.":PRINT
70 PRINT"KÉRI A HASZNÁLHATó SZAVKAT (I/N)?"
80 IF INKEY$=""THEN80
90 IF INKEY$="I"THENGOSUB 12000:CLS:GOTO93
91 IF INKEY$="N"THENCLS:GOTO93
92 GOTO80
93 CLEAR1000:SZ%=63:DIMUT%(4),U$(SZ%),TT%(21),O%(33,8):FORI=1 TO SZ%:READU$(I):NEXTI:FORI=1TO21:READTT%(I):NEXTI
94 FOR I=1 TO 33:FOR J=1 TO 8:READO%(I,J):NEXTJ,I
95 HE%=1:ON ERROR GOTO 20000
96 GOSUB 380:IF KI%=1THENPRINT"A KIRÁLYLÁNY BELÉD KAROLVA TIPEG MELLETTED"
97 IF KI%=8THENPRINT"A SZÉP KIRÁLYLÁNY BELÉDKAROLVA TIPEG."
100 L%=0:N$="":A$="":PRINTCHR$(6)"<";
110 A$=INKEY$:IFA$=""THEN110
111 BEEP2,500
112 IF A$=CHR$(12)THEN110
115 IF A$=CHR$(13)THENPRINTCHR$(8):GOTO150
120 IF A$=CHR$(8)ANDL%>0THENPRINTCHR$(8)CHR$(8)"<";:L%=L%-1:N$=LEFT$(N$,LEN(N$)-1):GOTO110
130 IF A$=CHR$(8)THEN110
140 PRINTCHR$(8)A$"<";:L%=L%+1:N$=N$+A$:GOTO110
150 N$=N$+" ":PRINTCHR$(8):IF N$=""THEN100
160 C$="":E%=0:FOR I=1 TO LEN(N$):IF MID$(N$,I,1)=" "THEN175
165 C$=C$+MID$(N$,I,1)
170 NEXT I:PRINT:GOTO250
175 IF C$=""THEN170
180 FOR J=1 TO SZ%:IF U$(J)=C$THENUT%(E%)=J:E%=E%+1:C$="":GOTO 170
190 NEXTJ:PRINT"NEM ÉRTEM EZT A SZóT: "CHR$(34)C$CHR$(34)".":GOTO100
250 IF UT%(0)=0 THEN 100
255 IF UT%(0)>0ANDUT%(0)<9 THEN 2000
260 ON UT%(0)-8GOTO300,300,340,340,600,600,630,630,650,650,730,760,770,780,800
270 ON UT%(0)-23GOTO 800,735,840,870,900,910,920,920,1060,930,980,980,1100,1200
280 ON UT%(0)-37GOTO1210,1280,1300,1300,1350
300 IF UT%(1)<43 THEN PRINT"MIT?":GOTO100
310 UT%(1)=UT%(1)-42:IF TT%(UT%(1))<>HE%THENPRINT"ITT ILYESMI NINCSEN!":GOTO100
315 IF UT%(1)=13THENPRINT"EZZEL AZ IZOMZATTAL?!":GOTO100
320 TT%(UT%(1))=0
330 PRINT"OK.":BEEP33,33:GOTO100
340 IF UT%(1)<43THENPRINT"MIT?":GOTO100
350 UT%(1)=UT%(1)-42:IF TT%(UT%(1))>0 THENPRINT"ELőBB SZEREZD MEG!":GOTO100
360 TT%(UT%(1))=HE%:IF HE%=16ANDUT%(1)=3ANDRA%=0THEN370
361 IF HE%=33ANDUT%(1)=14ANDMM%=0THEN375
362 GOTO330
370 PRINT"OK.":BEEP33,33:RA%=1:SS%=SS%+5:PRINT"AZ UTONÁLLóT úGY FEJBE NYOMTAD, HOGY      RÖGTÖN KINYúLT!":GOTO100
375 PRINT"OK.":BEEP33,33:SS%=SS%+5:MM%=1:PRINT"A KISMADÁRKA MEGKÖSZÖNI, HOGY MEGMENTETTED ÉS HÁLÁBóL NEKED AJÁNDÉKOZ EGY GYűRűT!":TT%(4)=0:GOTO100
380 ONHE%GOTO390,395,400,405,410,415,420,425,430,435,440,445,450,455,460,465,470,475,480,490,495,500,500,505,500,500,500,500,510,515,520,530
382 IF HE%=33THEN535
390 PRINT"A KUNYHóDBAN VAGY.":RETURN
395 PRINT"A KUNYHóD ELőTT ÁLLSZ.":RETURN
400 PRINT"EGY KÖVES ÖSVÉNYEN ÁLLSZ.":RETURN
405 PRINT"EGY ÁROK ELőTT TOPOROGSZ.":RETURN
410 PRINT"EGY TISZTÁSON VAGY":RETURN
415 PRINT"ÁTHATOLHATATLAN POKHÁLóSZÖVEDÉKET LÁTSZ."
416 IFTT%(5)=50THENPRINT"A HÁLóBAN VERGőDő LÉGYHEZ EGY FÖRTELMES   PóK KÖZELEDIK."
417 RETURN
420 PRINT"EGY úTKERESZTEZőDÉSHEZ ÉRTÉL.":RETURN
425 PRINT"EGY FA MELLETT ÁLLSZ.":RETURN
430 PRINT"EGY VÁROSKA FőTERÉN VAGY."
431 IFTT%(11)=50 THEN PRINT"EGY KOVÁCS NAGY HANGON KARDOT ÁRUL."
432 RETURN
435 PRINT"EGY SZűK SIKÁTORBAN VAGY.":RETURN
440 PRINT"A KIRÁLYI VÁR FELÉ VEZETő úTON HALADSZ.":RETURN
445 PRINT"A KIRÁLYI VÁR FELVONóHíDJÁNÁL ÁLLSZ.":RETURN
450 PRINT"A KIRÁLYI VÁRBAN VAGY. ITT VAN A KIRÁLY."
451 IF KI%=1THEN4000
452 PRINT"íGY SZóL HOZZÁD:"CHR$(32)CHR$(34)"ÜDVÖZÖLLEK, BÁTOR LOVAG,SZÉP A RUHÁD, HOL A LOVAD? MI SZÉL HOZOTT,MONDSZA, ERRE, S MI VAJON A SZíVED TERVE?"
453 PRINT"BIBIRCSóK AZ ÉN NEVEM ELRABOLTÁK GYERMEKEMARRA KÉRLEK BÁTOR LOVAG, HOZZD VISSZA A   KISLÁNYOMAT":PRINT"JUTALMUL ÉN BÁRMIT ÁDOK, FELE LÁNYT ÉS    KIRÁLYSÁGOT!"CHR$(34)
454 RETURN
455 PRINT"EGY PISZKOS NYOMORúSÁGOS KUNYHóBAN VAGY.  EGY ÖREGASSZONY VAN ITT."
456 IFTT%(2)<50THENPRINT"íGY SZóL HOZZÁD:"CHR$(34)" MIT BÁMULSZ TE NYAMVADT LOVAG ÉHEN HALOK GYOMROM LYUKAD!"CHR$(34)
457 RETURN
460 PRINT"EGY HEGY LÁBÁHOZ ÉRKEZTÉL.":RETURN
465 PRINT"EGY MEREDEK HEGYI ÖSVÉNYEN VAGY."
466 IF RA%=0THEN PRINT"EGY MÉRGES UTONÁLLó ÁLL ELőTTED!"
467 RETURN
470 PRINT"EGY MEREDEK SZAKADÉK SZÉLÉN ÁLLSZ.":RETURN
475 PRINT"EGY BARLANG BEJÁRATÁNÁL ÁLLSZ, AMELYBőL   FÖRTELMES KÉNHIDROGÉN BűZ ÁRAD."
476 PRINT"ÖSVÉNY VEZET EGY SZIKLAPADRA.":RETURN
480 PRINT"EGY BARLANGBA ÉRTÉL, A HőSÉG ELVISELHETET-LEN."
481 IF SA%=0THENPRINT"EGY VÉRES,NYÁLKÁS SÁRKÁNY ÁLL ELőTTED!"
482 IF SA%=1 THENPRINT"EGY SÁRKÁNY TEHETETLENÜL VERGőDIK EGY     SZIKLA ALATT!"
483 IFSA%=2THENPRINT"EGY SÁRKÁNYTETEMET LÁTSZ."
484 IFKI%=0THENPRINT"A KIRÁLYLÁNY HOLTAN FEKSZIK A SAROKBAN."
485 RETURN
490 PRINT"EGY SZIKLAPADON VAGY AMELYRőL A BARLANG   TETEJÉRE LEHET MÁSZNI.":RETURN
495 PRINT"A BARLANG TETEJÉN VAGY, EGY NAGY KÖRALAKú NYíLÁSON BE LÁTSZ A BARLANGBA.":RETURN
500 PRINT"EGY úTTALAN SÖTÉT ERDőBEN BARANGOLSZ.":RETURN
505 PRINT"EGY FOLYó MELLETT VAGY.":IFFU%=0THENPRINT"EGY FULDOKLó ÖRJÖNGVE SEGíTSÉGÉRT KIÁLT."
506 RETURN
510 PRINT"EGY ELHAGYATOTT FÉSZERBEN VAGY.":RETURN
515 PRINT"EGY RÉTEN VAGY, EGY HÁZIKóT LÁTSZ.":RETURN
520 PRINT"A HÁZIKóBAN VAGY. EGY ASZTAL ÁLL ELőTTED."
521 IFTT%(10)=50 THEN PRINT"AZ ASZTALON HÁROM LOMBIK SORAKOZIK.":GOTO523
522 RETURN
523 PRINT"A HÁZIASSZONY ( MELLÉKESEN OCSMÁNY,       FOGLALKOZÁSA: BOSZORKÁNY ) íGY SZóL HOZZÁD"
524 PRINTCHR$(34)"AZ EGYIK LOMBIKBAN AZ ÉLET VIZE FORTYOG.  HA ELTALÁLóD MELYIK AZ, NEKED ADOM, DE HA NEM, HITVESEM LESZEL!"CHR$(34):RETURN
530 PRINT"EGY MEZőRE ÉRTÉL KI A BARLANGBóL.":RETURN
535 PRINT"A FA KORONÁJÁBAN VAGY. EGY FÉSZKET LÁTSZ. ( HA A LEVÉLTETVEK IGENCSAK ELSZAPORODNAK HASZNÁLJON BI-56 PERMETEZőSZERT, EZT      AJÁNLJA AZ AGROKER )":RETURN
600 PRINT"NÁLAD VAN: ":C%=0:FORJ=1TO 14:IFTT%(J)=0THENPRINTTAB(10)U$(J+42):C%=1
610 NEXTJ:IF C%=0THENPRINTTAB(10)"SEMMI AZ ÉG VILÁGON."
620 GOTO 100
630 PRINT"EZT LÁTOD: ":C%=0:FORJ=1TO14:IFTT%(J)=HE%THENPRINTTAB(10)U$(J+42):C%=1
635 NEXTJ:IFC%=0THENPRINTTAB(10)"SEMMI KÜLÖNÖS DOLOG"
640 GOTO 100
650 IF UT%(1)=0THENPRINT"MIT?":GOTO100
660 IF UT%(1)<43 THENPRINT"JóL VAGY?":GOTO100
665 IF TT%(UT%(1)-42)<>HE%ANDTT%(UT%(1)-42)<>0 THEN 675
670 ONUT%(1)-42GOTO675,675,680,685,690,695,700,705,710,675,715,675,720,725
675 PRINT"SEMMI FONTOSAT NEM LÁTSZ.":GOTO100
680 PRINT"HA JóL MEGDOBJÁK AGYON ÜTHET VALAKIT!":GOTO100
685 IF HE%=17THEN PRINT"PRóBÁLD FEL.":GOTO100
686 GOTO 675
690 IF HE%=31THENPRINT"FúJHATó.":GOTO100
691 GOTO675
695 PRINT"EGY LÁDIKó KULCSA!":GOTO100
700 PRINT"VAN NÁLAD KULCS?":GOTO100
705 PRINT"SAJNOS CSAK ARANYÉRT KAPHATSZ KARDOT!":GOTO100
710 IF HE%=27THENPRINT"ITT ÁSS!":GOTO100
711 PRINT"ITT NEM ÉRDEMES ÁSNI!":GOTO100
715 PRINT"A CSEL CÉLRAVEZETőBB A NYíLT TÁMADÁSNÁL!":GOTO100
720 PRINT"MOZDíTHATó!":GOTO100
725 PRINT"JóTETT HELYÉBE JóT VÁRJ!":GOTO100
730 IF UT%(1)=57 THEN 740
735 PRINT"NEM TEHETSZ ILYET!":GOTO100
740 IF HE%<>24ORFU%=1THENPRINT"MIÉRT TALÁN TE FULDOKOLSZ?":GOTO100
745 FU%=1:SS%=SS%+5:PRINT"A KIMENTETT EMBER MEGKÖSZÖNI A SEGíTSÉGE- DET ÉS EGY KULCSOT AJÁNDÉKOZ NEKED, MAJD  úSZNI INDUL.":TT%(6)=0:GOTO100
760 IFUT%(1)=0THENPRINT"MIT?":GOTO100
761 IF UT%(1)<>44THENPRINT"AZ ILYEN MENJEN LIBÁT ASZALNI!":GOTO100
762 IF TT%(2)>0THEN766
765 TT%(2)=50:GOTO330
766 PRINT"NINCS NÁLAD.":GOTO100
770 PRINT"EREDMÉNYED: "SS%"%":GOTO100
780 IF UT%(1)<>58THEN735
785 IFHE%=8THENHE%=33:PRINT"OK.":BEEP33,33:GOTO96
790 PRINT"LÁZASS VAGY? EZT NE TEDD!":GOTO100
800 IFUT%(1)=54THEN815
805 IFUT%(1)=46THEN825
810 :
815 IFTT%(12)>0 THEN 766
820 PRINT"OK.":SS%=SS%+5:TT%(12)=40:GOSUB19000:GOTO100
825 IF TT%(4)>0 THEN 766
826 IF HE%<>17THENPRINT"SEMMI SEM TÖRTÉNT!":GOTO100
830 PRINT"OK.":SS%=SS%+5:HE%=18
835 PRINT"HIRTELEN FELKAP KÉT MADÁR ÉS A SZAKADÉK   FELETT ÁTREPíTENEK!":GOSUB19000:GOTO96
840 IF UT%(1)=0THENPRINT"MIT?":GOTO100
845 IF UT%(1)<>49 THEN 735
850 IF TT%(7)>0 THEN 766
851 IF LA%=1 THEN PRINT"MÁR NYITVA VAN!":GOTO100
855 IF TT%(6)>0 THENPRINT"ZÁRVA VAN!":GOTO100
860 PRINT"OK.":BEEP33,33:PRINT"A LÁDÁBóL RENGETEG ARANY SZóRóDOTT KI.":LA%=1:TT%(8)=HE%:SS%=SS%+5:GOSUB19000:GOTO100
870 IF UT%(1)<>49THEN735
875 IFTT%(7)>0 THEN 766
880 IF LA%=0THENPRINT"MÉG KI SINCS NYITVA!":GOTO100
885 PRINT"úGY LÁTSZIK ELROMLOTT A ZÁR!":GOTO100
900 IFTT%(UT%(1)-42)>0THEN766
905 PRINT"MEGNYOMKODTAD.":GOTO955
910 PRINT"FEL AKAROD ADNI (I/N)?"
911 IF INKEY$="I"THEN3010:ELSEIFINKEY$<>"N"THEN911:ELSEGOTO100
920 PRINT"SZóSZÁTYÁR FICKóVAL SEMMIRE NEM LEHET     JUTNI!":GOTO100
930 IF UT%(1)<43 THENPRINT"MIT?":GOTO100
935 UT%(1)=UT%(1)-42:IFTT%(UT%(1))>0THEN76
940 IF UT%(1)<>5THEN735
945 PRINT"OK.":BEEP33,33:PRINT"AKKORÁT SíPOLTÁL MINT ZSOLT JÁTÉKVEZETő! "
950 IF HE%=31ANDTT%(10)=50THEN960
955 IF UT%(1)<>5 THENBEEP33,33:GOTO100:ELSEPRINT"NÉHÁNY MADÁR FELREBBENT, DE SEMMI NEM TÖR-TÉNT!":GOTO100
960 PRINT"VÁRATLANUL ELőRÖPPEN A LÉGY, RÖVID        TÉTOVÁZÁS UTÁN ODAREPPEN AZ EGYIK LOMBIK  FÖLÉ ÉS MEGÁLL. TEHÁT MEGMUTATTA MELYIK AZÉLET VIZE. MÁR CSAK MEG KELL FOGNI!":TT%(10)=31:SS%=SS%+5:GOSUB19000
961 GOTO 100
980 IF UT%(1)<59 THEN735
985 UT%(1)=UT%(1)-58:IF HE%=31ANDUT%(1)=1THEN1015
990 IF HE%=16ANDUT%(1)=2THEN1020
995 IFHE%=19ANDUT%(1)=3THEN1030
1000 IF HE%=6ANDUT%(1)=4 THEN1040
1005 IFUT%(1)=5THENPRINT"EGYEM MEG AZT A FANTÁZIADúS SZÜRKE ÁLOMÁ- NYODAT!":GOTO100
1010 GOTO 735
1015 PRINT"A BOSZORKÁNY AZ OLTÁR ELÉ AKAR VONSZOLNI  DE AZ ENGEDETLENSÉGED MIATT KőVÉ VÁLTOZTAT":GOTO3000
1020 PRINT"AZ UTONÁLLó HIRTEL MOZDULATTAL BICSKÁT KAPELő ÉS 34 DÖFÉS UTÁN HOLTAN ROGYSZ ÖSSZE!":GOTO3000
1030 IF SA%=2 THEN PRINT"MÁR RÉG BEKREPÁLT!":GOTO100
1032 IFTT%(11)>0THENPRINT"KARD NÉLKÜL EGY SÁRKÁNYRA RONTANI!":GOTO3000
1034 IF SA%=0THENPRINT"HIÁBA VAN KARDOD, EGY ÉLETERőS TÜZE ELLEN NEM VÉDHET MEG!":GOTO3000
1036 PRINT"ÉLES PENGÉDDEL MEGADTAD A KEGYELEMDÖFÉST  A SZÖRNYNEK!":SA%=2:SS%=SS%+5:GOSUB19000:GOTO100
1040 IF TT%(5)<50THENPRINT"MÁR MEGTETTED!":GOTO100
1041 IF TT%(1)>0THENPRINT"PUSZTA KÉZZEL?":GOTO100
1045 PRINT"A BOTODDAL AGYONVERTED A PóKOT. A LÉGY    HÁLÁBóL EGY IGAZI JÁTÉKVEZETőI SíPOT AD   NEKED.":SS%=SS%+5:TT%(5)=0:GOSUB19000:GOTO100
1060 IF TT%(9)>0 THENPRINT"EGY SíRÁSó ÁSó NÉLKÜL!?":GOTO100
1065 PRINT"OK.":BEEP33,33:IF HE%<>27ORTT%(7)<50THEN955
1070 PRINT"EGY LÁDIKóT TALÁLTÁL!":SS%=SS%+5:TT%(7)=27:GOSUB19000:GOTO100
1100 IF UT%(1)=52 THEN1140
1110 IF UT%(1)=51 THEN1060
1120 21589 %(1)=53 ANDHE%=19THEN1030
1130 IF UT%(1)=0THENPRINT"MIT?":GOTO100
1135 GOTO 735
1140 IF TT%(10)>0THEN766
1141 IF HE%<>19 THEN PRINT"MIRE?":GOTO100
1145 PRINT"AZ ÉLET VIZÉVEL JóL ELÁZTATTAD A KIRÁLY-  LÁNY TOALETTJÉT AKI EZÉRT ÉLETREKELÉS UTÁNMEGDORGÁL,  DE AZÉRT PUSZIT IS KAPSZ!     MOST MÁR CSAK A KIRÁLYNAK KELL ÁLDÁSÁT    ADNIA A NÁSZRA!":KI%=1
1165 GOSUB19000:SS%=SS%+5:GOTO100
1200 PRINT"HOGYAN?":GOTO100
1210 IF UT%(1)<43THENPRINT"MIT?":GOTO100
1220 IFTT%(UT%(1)-42)>0THEN766
1230 IFUT%(1)<>44ORHE%<>14ORTT%(12)<50THENPRINT"NEM KELL SENKINEK!":GOTO100
1240 PRINT"AZ ÖREGASSZONY JóL BEKAJÁL ÉS CSERÉBE ADJAHALOTT FÉRJE PÁNCÉLJÁT!":GOSUB19000:SS%=SS%+5:TT%(12)=0:TT%(2)=50:GOTO100
1280 IF UT%(1)<>53ORHE%<>9THEN735
1285 IFTT%(8)>0THENPRINT"NINCS RÁ PÉNZED!":GOTO100
1290 GOSUB19000:TT%(8)=40:TT%(11)=0:SS%=SS%+5:PRINT"OK.":GOTO100
1300 IF UT%(1)<>55THEN735
1310 IFHE%<>TT%(13)THENPRINT"NEM LÁTOK SZIKLÁT.":GOTO100
1320 PRINT"OK.":BEEP33,33:PRINT"A SZIKLA NAGY ROBAJJAL LEZUHANT A BARLANG-BA!":TT%(13)=50
1330 GOSUB19000:SA%=1:SS%=SS%+5:GOTO100
1350 PRINT"HOPP!":IF HE%<>4THEN100
1355 PRINT"ÁTTUGROTTAD AZ ÁRKOT!":HE%=5:GOTO96
2000 IFO%(HE%,UT%(0))=0THEN PRINT"ARRA NEM MEHETSZ!":GOTO100
2005 IF HE%=16ANDRA%=0THEN1020
2006 IF O%(HE%,UT%(0))=50 THEN PRINT"TRILLALA TRILLALA A SZAKADÉKBA ZUHANTÁL!":GOTO3000
2007 IF HE%=4ANDUT%(0)=3THENPRINT"AZ UTADAT ELÁLJA AZ ÁROK                  (NEM ENGEDLEK TOVÁBB MERT MÉG MEGRÁNDíTOD A BOKÁDAT!)":GOTO100
2010 HE%=O%(HE%,UT%(0)):IF HE%<>19ORSA%=2THEN96
2020 IF SA%<>1THENPRINT"AHOGY BELÉPSZ EGY SÁRKÁNY CSAP FELÉD.":ELSE96
2025 IF TT%(12)=40 THENPRINT"SZERENCSÉD VAN, HOGY PÁNCÉLT VISELSZ! ":GOTO96
2030 PRINT"A GYENGE ÜTÉS ELLENÉRE IS A TESTED SZINTE KETTÉ SZAKAD..":GOTO3000
2085 GOTO 3000
2090 END     
3000 FORI=1TO3000:NEXTI:PRINT"ATTóL TARTOK MEGHALTÁL!"
3010 PRINTCHR$(2)"EREDMÉNYED: "SS%"%"CHR$(1):PRINT"úJ JÁTÉK(I/N)?"
3020 IF INKEY$="I"THENRUN:ELSEIFINKEY$<>"N"THEN3020:ELSE2090
4000 PRINT"MIRE VISSZA ÉRTÉL A KIRÁLY 10 ÉVET ÖREGE- DETT DE AZÉRT 1/4 óRA ALATT MEGÖLEL       TÉGED ÉS A KIRÁLYLÁNYT! ÁLDÁSÁT ADJA RÁTOKHATALMAS LAKODALOM, BOLDOG NÁSZúT KÖVETKE-ZIK. EZ SZÉP VOLT BÁTOR LOVAG!"
4010 GOSUB19000:GOSUB19000:SS%=SS%+30:GOTO 3010
10000 DATA É,D,K,NY,FEL,LE,BE,KI,FOG,FELVESZ,DOB,LETESZ,L,LISTA,N,NÉZ,V,VIZSGÁL,HúZ,ESZIK,%,MÁSZIK,VISEL,HORD,VÁG,NYIT,ZÁR,NYOM,FELAD,SZóL,MOND,ÁS,FúJ,ÖL,ÜT,HASZNÁL,MEGMENT,AD,VESZ,MOZDíT,LÖK,UGRIK
10010 20290 T,POGÁCSA,Kő,GYűRű,SíP,KULCS,LÁDIKó,ARANY,ÁSó,ÉLETVíZ,KARD,PÁNCÉL,SZIKLA,KISMADÁR,FULDOKLó,FA,BOSZORKÁNY,UTONÁLLó,SÁRKÁNY,PóK,KIRÁLYLÁNY
10070 DATA 1,1,3,50,50,50,50,50,29,50,50,50,21,8,50,50,60,60,60,60,60,0,0,0,0,0,0,0,2,1,0,3,0,0,0,1,0,0,0,4,2,0,0,0,0,0,0,5,3,0,0,0,0,6,0,7,4,0,0,0,0,0,5,0,0,0,0,0,0,0,22,8,5,0,0,0,0,0,0,9,7,33,0,0,0
10080 DATA10,0,15,8,0,0,0,0,0,9,14,11,0,0,14,0,12,0,10,0,0,0,0,0,0,11,0,0,13,0,13,0,0,12,0,0,0,0,0,12,0,0,0,0,0,0,0,10,0,16,0,9,16,0,0,0,15,0,17,0,0,15,0,0,0,0,50,16,0,0,0,0
10085 DATA 0,0,19,0,20,0,19,0,0,0,32,18,0,0,0,18,0,18,21,0,0,0,0,0,0,0,0,20,0,0,0,0,7,22,22,23,0,0,22,7,23,25,22,24,0,0
10090 DATA 23,23,0,0,23,0,0,0,23,0,23,25,26,25,0,0,25,25,26,28,27,25,0,0,26,26,0,0,0,26,0,0,0,0,26,30,28,29,0,0,28,30,0,0,28,0,0,0,0,28,28,0,31,0,0,0,31,0,0,0,0,30,0,0,0,30,0,15,0,19,21,0,19,0,0,0,0,0,0
10100 DATA8,0,0
12000 CLS:PRINT"AZ IGEKÉNT HASZNÁLHATó SZAVAK:"
12010 RESTORE10000:FORI=1TO42:READQ$:PRINTQ$",";:NEXTI:PRINTCHR$(6)CHR$(8):RESTORE10000
12020 PRINT"A TÁRGYAKAT úTKÖZBE MEGTUDJA!       ":PRINT"NYOMJON MEG VALAMIT"
12030 IF INKEY$=""THEN12030
12040 RETURN
19000 RETURN
20000 RESUME NEXT
